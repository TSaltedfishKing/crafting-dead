buildscript {
    repositories {
        maven { 
            name 'forge'
            url 'https://maven.minecraftforge.net/'
        }
        maven {
            name 'sponge'
            url 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

plugins {
    id 'java-library'
	id 'eclipse'
	id 'org.cadixdev.licenser' version '0.5.0'
}

subprojects {
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'org.spongepowered.mixin'
    apply plugin: 'eclipse'
    apply plugin: 'org.cadixdev.licenser'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    group = 'com.craftingdead'
    version = "${mod_version}"
    minecraft {
        mappings channel: 'official', version: "${minecraft_version}"
    }

    repositories {
        maven {
            name 'sponge'
            url 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    configurations {
        implementation.extendsFrom shaded
    }

    dependencies {
        minecraft group: 'net.minecraftforge', name:'forge', version: "${minecraft_version}-${forge_version}"
        implementation group: 'org.spongepowered', name: 'mixin', version: "${mixin_version}"
        annotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
        testAnnotationProcessor("org.spongepowered:mixin:${mixin_version}:processor")
    }

    jar {
        into('dependencies') {
            from  configurations.shaded
        }
        archiveBaseName = project.name
        archiveVersion = "${minecraft_version}-${version}"
        from rootProject.file('LICENSE.txt')
        finalizedBy('reobfJar') 
    }

    license {
        include '**/*.java'

        header = rootProject.file('HEADER.txt')

        ext {
            organization = 'NexusNode LTD'
            year = 2021
        }
    }    
}